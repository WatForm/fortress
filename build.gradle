apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'antlr'

repositories{
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    compile "org.antlr:antlr4-runtime:4.7.2"
    compile group: 'com.oath.cyclops', name: 'cyclops', version: '10.1.0'
    compile files('z3/com.microsoft.z3.jar')
    runtime files('z3/libz3java.dylib') // Required to run Z3's Java interface
    testCompile 'junit:junit:4.12'
}

// JUNIT

test {
    ignoreFailures true // Don't stop build when unit test fails
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(Test) {
    // Allow junit test runtime to find libz3java.dylib
    systemProperty "java.library.path", "z3/"
}

// ANTLR

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
    arguments += ['-package', 'fortress.formats'] // Add fortress.formats package header to files
}

// Zip up both the main fortress jar and any runtime dependencies
// Note that these are kept as separate jars in the zip file, not one fat jar
distributions {
    main {
        contents {
            from configurations.runtime
            from jar
        }
    }
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'jacoco'
apply plugin: 'pmd'

repositories{
    mavenCentral()
}

dependencies {
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'
    compile group: 'com.oath.cyclops', name: 'cyclops', version: '10.1.0'
    compile files('z3/com.microsoft.z3.jar')
    runtime files('libz3java.dylib')
    testCompile 'junit:junit:4.12'
}

test {
    ignoreFailures true // Don't stop build when unit test fails
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Zip up both the main fortress jar and any runtime dependencies
// Note that these are kept as separate jars in the zip file, not one fat jar
distributions {
    main {
        contents {
            from configurations.runtime
            from jar
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}

jacocoTestReport.dependsOn test
build.dependsOn jacocoTestReport
build.dependsOn javadoc

tasks.withType(Pmd) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Test) {
    // Allow junit test runtime to find libz3java.dylib
    systemProperty "java.library.path", "z3/"
}

pmd {
    toolVersion = '6.1.0'
    ignoreFailures true // Don't prevent the build from continuing, but still report errors
    pmdTest.enabled false
    ruleSets = [
        'pmd/custom.xml'
    ]
}
